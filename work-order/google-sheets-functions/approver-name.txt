=ARRAYFORMULA(
  IF(
    ROW(BK:BK)=1, "Approver Name",

    IF(
      ISBLANK(G:G),

      IF(
        ISBLANK(M:M),

        IF(
          ISBLANK(P:P), "ERROR",

          IFS(
            (P:P="WITHIN monthly YTD marketing budget"), "ISS Accounting",
            (P:P="OVER monthly YTD marketing budget"), "Justin Frank",
            (P:P="WILL EXCEED TOTAL ANNUAL MARKETING BUDGET"), "Charles Oliver"
          )
        ),

        IFS(
          ((M:M="WITHIN monthly YTD budget & UNDER $500")+
          (M:M="WITHIN monthly YTD budget & OVER $500")+
          (M:M="OVER monthly YTD budget & UNDER $500")),
            "ISS Accounting",
          ((M:M="OVER monthly YTD budget & OVER $500")+
          (M:M="WILL EXCEED TOTAL ANNUAL BUDGET")),
            "Charles Oliver"
        )
      ),

      IFS(
        (G:G="WITHIN my discretionary monthly allotment"), "ISS Accounting",
        (G:G="OVER my discretionary monthly allotment"), "Justin Frank"
      )
    )
  )
)