=ARRAYFORMULA(
  IF(
    ROW(AW:AW)=1, "Multiple Vendors",
    
    IF(
      (M:M="WITHIN monthly YTD budget & OVER $500")+
      (M:M="OVER monthly YTD budget & OVER $500")+
      (O:O="OVER monthly YTD marketing budget"),

      IF(
        ISBLANK(S:S),

        "Bid(s): " & Q:Q & ", Vendor Preference Explanation: " & R:R,

        "Bid(s): " & Q:Q & ", Vendor Preference Explanation: " & R:R & char(10) &

        REGEXREPLACE(
          REGEXREPLACE(
            REGEXREPLACE(
              S:S, "$", ">Quote</a>"
            ),
          
            ", ", ">Quote</a>" & " - " & "<a href="
          ),
      
          "^", "<a href="
        )
      ),

      "N/A"
    ) 
  )
)