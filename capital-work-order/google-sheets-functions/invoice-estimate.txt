=ARRAYFORMULA(
  IF(
    ROW(AN:AN)=1, "Invoice/Estimate",
    
    IF(
      (REGEXMATCH(T:T, "No")+ISBLANK(T:T)), "N/A",

      IF(
        REGEXMATCH(T:T, "attaching below"),

        IF(
          ISBLANK(U:U),
          
          T:T & " - ERROR: No file attached!",

          T:T & char(10) &

          REGEXREPLACE(
            REGEXREPLACE(
              REGEXREPLACE(
                U:U, "$", ">Invoice</a>"
              ),
            
              ", ", ">Invoice</a>" & " - " & "<a href="
            ),
        
            "^", "<a href="
          )
        ),

        "Yes - attached in 'Multiple Vendors' section above."
      )
    )
  )
)