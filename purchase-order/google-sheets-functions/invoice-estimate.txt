=ARRAYFORMULA(
  IF(
    ROW(AS:AS)=1, "Invoice/Estimate",
    
    IF(
      (REGEXMATCH(AC:AC, "No")+ISBLANK(AC:AC)), "N/A",

      IF(
        REGEXMATCH(AC:AC, "attach below"), 

        IF(
          ISBLANK(AD:AD),

          AC:AC & " - ERROR: No file attached!",

          AC:AC & char(10) &

          REGEXREPLACE(
            REGEXREPLACE(
              REGEXREPLACE(
                AD:AD, "$", ">Invoice</a>"
              ),
            
              ", ", ">Invoice</a>" & " - " & "<a href="
            ),
        
            "^", "<a href="
          )
        ),

        "Yes - attached in 'Multiple Vendors' section above."
      )
    )
  )
)