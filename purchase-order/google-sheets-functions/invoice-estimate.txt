=ARRAYFORMULA(
  IF(
    ROW(AV:AV)=1, "Invoice/Estimate",
    
    IF(
      (REGEXMATCH(AF:AF, "No")+ISBLANK(AF:AF)), "N/A",

      IF(
        REGEXMATCH(AF:AF, "attaching below"),

        IF(
          ISBLANK(AG:AG),

          "Yes, attached here: " & "[ERROR: No file attached!]",

          "Yes, attached here: " & "<a href=" & AG:AG & ">Attachment</a>"
        ),

        IFS(
          REGEXMATCH(AF:AF, "Multiple Vendors"), "Yes - attached in the 'Multiple Vendors' section above.",
          REGEXMATCH(AF:AF, "SIGNED Invoice"), "INVOICE(S)/ESTIMATE(S) TO BE SIGNED: " & char(10) &

          REGEXREPLACE(
            REGEXREPLACE(
              REGEXREPLACE(
                U:U, "$", ">Attachment</a>"
              ),
            
              ", ", ">Attachment</a>" & " - " & "<a href="
            ),
        
            "^", "<a href="
          )
        )
      )
    )
  )
)